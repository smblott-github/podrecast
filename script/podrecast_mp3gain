#!/usr/bin/env zsh
# vim: ft=zsh

local suffix='.podrecast_download'
local gain=6

# ###########################################################################
# Customisation...
#
# If ~/.podrecast_mp3gain exists and is executable, then it is sourced
# here.  This provides an opportunity for customisation, such as renaming
# or moving files.
#

local rc="$HOME/.podrecast_mp3gain"

if [[ -f $rc && -s $rc && -x $rc ]]
then
   print "source: $rc"
   source $rc
fi

# ###########################################################################
# Do we have mp3gain...?

if ! which mp3gain > /dev/null
then
   print "error: could not find mp3gain executable" >&2
   exit 1
fi

# ###########################################################################
# Do mp3gain...

local -a extras

if ! [[ -t 1 ]]
then
   extras+=( '-q' )
fi

__do_mp3gain ()
{
   local src="$argv[1]"
   local dst="${src%$suffix}"

   print "from:" $src
   print "to:" $dst
   (
      if flock -x -n 9
      then
         nice nice mp3gain -r -c -p -t -m $gain $extras $src \
            && [[ $src != $dst ]]                            \
            && mv -v $src $dst
      else
         print "failed to lock:" $src
      fi
   ) 9< $src
}

# ###########################################################################
# Apply __do_mp3gain...

autoload -U zargs
zargs -r -L 1 -- $( podrecast -f ) -- __do_mp3gain

