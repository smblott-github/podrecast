#!/usr/bin/env zsh
# vim: ft=zsh

local gain=6
local suffix='.podrecast_download'

# ###########################################################################
# Do we have mp3gain...?

if ! which mp3gain > /dev/null
then
   print "error: could not find mp3gain executable" >&2
   exit 1
fi

# ###########################################################################
# Find config file...

local config

() {
   local conf
   for conf
      [[ -z $config && -n $conf && -f $conf && -r $conf ]] && config=$conf
} ./podrecast.conf ~/.podrecast.conf /usr/local/etc/podrecast.conf /etc/podrecast.conf

if [[ -z $config ]]
then
   print "error: could not find podrecast.conf" >&2
   exit 1
fi

print "config: $config"

# ###########################################################################
# Find roots...

local -a roots

grep -v '^\s*#' $config    \
   | sed -r 's/^\s*//'     \
   | sed -r 's/\s{1,}/ /'  \
   | sed -n '/root\>/ p'   \
   | cut -d' ' -f 2        \
   | read -A roots

print "roots:" $roots

if (( $#roots == 0 ))
then
   print "error: could not find any root directories" >&2
   exit 1
fi

# ###########################################################################
# Customisation...
#
# If ~/.podrecast_mp3gain exists and is executable, then it is sourced
# here.  This provides an opportunity for customisation, such as renaming
# or moving files.
#
# Note: $roots is already set, here.  So it can be used by such
# customisation scripts.  Indeed, it can even be changed.
#

local rc="$HOME/.podrecast_mp3gain"

if [[ -f $rc && -s $rc && -x $rc ]]
then
   print "source: $rc"
   source $rc
fi

# ###########################################################################
# Do mp3gain...

local -a extras

if ! [[ -t 1 ]]
then
   extras+=( '-q' )
fi

__do_mp3gain ()
{
   local src="$argv[1]"
   local dst="${src%$suffix}"

   print "from:" $src
   print "to:" $dst
   (
      if flock -x -n 9
      then
         nice nice mp3gain -r -c -p -t -m $gain $extras $src \
            && [[ $src != $dst ]]                            \
            && mv -v $src $dst
      else
         print "failed to lock:" $src
      fi
   ) 9< $src
}

# ###########################################################################
# Apply __do_mp3gain...

local root
autoload -U zargs

print "suffix:" $suffix

for root in $roots
   zargs -r -L 1 -- $root/**/*$suffix(N.) -- __do_mp3gain

